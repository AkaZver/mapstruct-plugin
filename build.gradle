plugins {
    id 'com.gradle.plugin-publish' version '1.3.1'
    id 'io.freefair.lombok' version '8.13.1'
    id 'org.sonarqube' version '6.1.0.5360'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
}

group = 'com.github.akazver.mapstruct'
version = '1.0.9'

gradlePlugin {
    website = 'https://github.com/AkaZver'
    vcsUrl = 'https://github.com/AkaZver/mapstruct-plugin'

    plugins {
        mapstruct {
            id = 'com.github.akazver.mapstruct'
            displayName = 'MapStruct Plugin'
            description = 'Automatic MapStruct configuration'
            implementationClass = 'com.github.akazver.gradle.plugins.mapstruct.MapstructPlugin'
            tags = ['mapstruct', 'mapping', 'lombok', 'spring', 'quarkus', 'camel']
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'io.freefair.gradle:lombok-plugin:8.13.1'
    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.1'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.12.1'
    testImplementation 'org.mockito:mockito-core:5.16.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.16.1'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

java {
    sourceCompatibility(8)
    targetCompatibility(8)
    withSourcesJar()
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "AkaZver_mapstruct-plugin"
        property "sonar.organization", "aka-zver"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}
