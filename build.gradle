plugins {
    id 'com.gradle.plugin-publish' version '0.20.0'
    id 'org.sonarqube' version '3.3'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
}

group = 'com.github.akazver.mapstruct'
version = '1.0.1'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

pluginBundle {
    website = 'https://github.com/AkaZver'
    vcsUrl = 'https://github.com/AkaZver/mapstruct-plugin'
    tags = ['mapstruct']
}

gradlePlugin {
    plugins {
        mapstruct {
            id = 'com.github.akazver.mapstruct'
            displayName = 'Mapstruct Plugin'
            description = 'Automatic mapstruct configuration'
            implementationClass = 'com.github.akazver.gradle.plugins.mapstruct.MapstructPlugin'
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    systemProperty 'java.util.logging.config.file', "${buildDir}/resources/test/logging-test.properties"
}

java {
    withSourcesJar()
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "AkaZver_mapstruct-plugin"
        property "sonar.organization", "aka-zver"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
