plugins {
    id 'com.gradle.plugin-publish' version '1.2.0'
    id 'io.freefair.lombok' version '6.6.3'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
}

group = 'com.github.akazver.mapstruct'
version = '1.0.5'

pluginBundle {
    website = 'https://github.com/AkaZver'
    vcsUrl = 'https://github.com/AkaZver/mapstruct-plugin'
    tags = ['mapstruct']
}

gradlePlugin {
    plugins {
        mapstruct {
            id = 'com.github.akazver.mapstruct'
            displayName = 'Mapstruct Plugin'
            description = 'Automatic Mapstruct configuration'
            implementationClass = 'com.github.akazver.gradle.plugins.mapstruct.MapstructPlugin'
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'io.freefair.gradle:lombok-plugin:6.6.3'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

java {
    sourceCompatibility(8)
    targetCompatibility(8)
    withSourcesJar()
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "AkaZver_mapstruct-plugin"
        property "sonar.organization", "aka-zver"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
