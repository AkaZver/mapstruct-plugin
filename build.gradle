plugins {
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'io.freefair.lombok' version '8.7.1'
    id 'org.sonarqube' version '5.1.0.4882'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
}

group = 'com.github.akazver.mapstruct'
version = '1.0.8'

gradlePlugin {
    website = 'https://github.com/AkaZver'
    vcsUrl = 'https://github.com/AkaZver/mapstruct-plugin'

    plugins {
        mapstruct {
            id = 'com.github.akazver.mapstruct'
            displayName = 'MapStruct Plugin'
            description = 'Automatic MapStruct configuration'
            implementationClass = 'com.github.akazver.gradle.plugins.mapstruct.MapstructPlugin'
            tags = ['mapstruct', 'mapping', 'lombok', 'spring', 'quarkus', 'camel']
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'io.freefair.gradle:lombok-plugin:8.7.1'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.0'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

java {
    sourceCompatibility(8)
    targetCompatibility(8)
    withSourcesJar()
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "AkaZver_mapstruct-plugin"
        property "sonar.organization", "aka-zver"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}
